apply plugin: 'com.android.application'
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.dus.gradle"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        multiDexEnabled true // 突破方法数限制65535
        manifestPlaceholders = [UMENG_CHANNEL_VALUE :"umeng"]// 默认打包渠道

    }
    signingConfigs {
        debug {
            // 不需要签名
        }
        release {
            storeFile file("gradle-sign.jks")
            storePassword "ds62340"
            keyAlias "gradle"
            keyPassword "ds62340"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "dus${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        "xiaomi" {}
        "360" {}
        "wandoujia" {}
    }

    productFlavors.all{
        productFlavor -> productFlavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
}
